buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = "Modmuss50"
            url = "http://maven.modmuss50.me/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:spongegradle:0.8.1'
        classpath 'ModsIOUpload:ModsIOUpload:+'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.gradle'
apply plugin: 'org.spongepowered.plugin'
apply plugin: 'net.cazzar.gradle.modsio'

version = "1.4.1"
group = "the_fireplace.clans"
archivesBaseName = "Clans-1.12.2"

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    replace '${version}', project.version
    mappings = "stable_39"
	
	clientRunArgs += "--uuid=0b1ec5ad-cb2a-43b7-995d-889320eb2e5b --username=The_Fireplace"
}

dependencies {
    compile group: "net.shadowfacts", name: "Forgelin", version: "1.8.2"
    deobfProvided "us.dynmap:DynmapCoreAPI:3.0-SNAPSHOT"
    deobfProvided "journeymap-api:journeymap-api:1.12:1.4"
    provided 'org.spongepowered:spongeapi:7.1.0'
}

repositories {
    jcenter()
    mavenCentral()
    // Sponge
    maven { url 'https://repo.spongepowered.org/maven' }
    // Forgelin
	maven { url "http://maven.shadowfacts.net/" }
	// Dynmap
    maven { url "http://repo.mikeprimm.com" }
    //iChunUtil, JourneyMap API, Chisels & Bits
    maven { url "https://minecraft.curseforge.com/api/maven/" }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    if (project.hasProperty("alias") && project.hasProperty("storePass") && project.hasProperty("keyPass") && project.hasProperty("keyStore")) {
        alias = project.alias
        storePass = project.storePass
        keyPass = project.keyPass
        keyStore = project.keyStore

        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

build.dependsOn signJar

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

curseforge {
  apiKey = project.hasProperty("curseForgeApiKey")? project.curseForgeApiKey : "empty"
  project {
    id = '313915'
    changelog = file('changelog.txt')
    releaseType = 'release'
    addGameVersion '1.12.2'
    addGameVersion '1.12.1'
    addGameVersion '1.12'
	addGameVersion 'Java 8'
    mainArtifact(jar) {
        displayName = jar.archiveName.replace(".jar", "")
        relations{
            optionalLibrary 'grand-economy'
            optionalLibrary 'dynmapforge'
        }
    }
    addArtifact(sourceJar) {
        displayName = sourceJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
    addArtifact(deobfJar) {
        displayName = deobfJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
  }
}

modsIO {
    key = project.hasProperty("modsioApiKey")? project.modsioApiKey : "empty"
    project{
		artifact = new File(jar.destinationDir, archivesBaseName+"-"+version+".jar")
		modid = "1280"
		minecraft = "1.12.2"
		try{changelog = file('changelog.txt').text}catch(Exception ignored){changelog=""}
		tag = ""
		current = true
    }
}
